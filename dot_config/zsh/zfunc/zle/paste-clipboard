#!/usr/bin/env zsh

# ZLE widget: paste clipboard content at the cursor.
# - Success even if the content is empty (does nothing, but not an error).
# - Show an error message only when the provider fails.

function paste-clipboard {
  emulate -L zsh
  setopt no_aliases

  # Load helper on demand (it self-unloads inside).
  local -r self_dir=${${functions_source[${funcstack[1]}]}:A:h}
  autoload -Uz "$self_dir/lib/__get_clipboard_content" || { zle -M "Failed to load clipboard helper"; return 1; }

  # Fetch clipboard; provider failure => non-zero.
  local rc=0 clipboard_content
  clipboard_content=$(__get_clipboard_content) || rc=$?

  if (( rc != 0 )); then
    zle -M "Failed to paste clipboard content"
    return 1
  fi

  # Empty is a valid success: do nothing quietly.
  if [[ -n $clipboard_content ]]; then
    LBUFFER+="$clipboard_content"

    # immediate highlighting if zsh-syntax-highlighting is present.
    (( ${+functions[_zsh_highlight]} )) && _zsh_highlight

    zle -R  # refresh line to reflect changes immediately
  fi

  return 0
}
